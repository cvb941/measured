buildscript {
    ext.kotlin_version = '1.3.70'
    ext.dokka_version  = '0.9.16'

    repositories {
        maven       { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-multiplatform'

version  = '1.0'
group    = 'com.nectar.measured'

repositories {
    maven       { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
    jcenter     ()
}

apply from: 'dependencies.gradle'

kotlin {
//    jvm ()
//    js { browser {} }

    targets {
        fromPreset(presets.js,  'js' )
        fromPreset(presets.jvm, 'jvm')
    }

    [compileKotlinMetadata, compileKotlinJvm, compileTestKotlinJvm, compileKotlinJs, compileTestKotlinJs]*.configure {
        kotlinOptions.freeCompilerArgs = ["-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"]
    }

    [compileKotlinJs, compileTestKotlinJs]*.configure {
        kotlinOptions {
            sourceMap             = true
            sourceMapEmbedSources = 'always'
            moduleKind            = 'commonjs'
        }
    }

    sourceSets {
        commonMain.dependencies {
            implementation dep.kotlin_stdlib_common
        }

        commonTest.dependencies {
            implementation dep.kotlin_test_common
            implementation dep.kotlin_test_annotations_common
            implementation dep.mockk_common
        }

        jvmMain.dependencies {
            implementation dep.kotlin_stdlib_jdk8
        }

        jvmTest.dependencies {
            implementation dep.junit
            implementation dep.kotlin_test_junit
        }

        jsMain.dependencies {
            implementation dep.kotlin_stdlib_js
        }

        jsTest.dependencies {
            implementation dep.mockk_js
            implementation dep.kotlin_test_js
        }
    }
}